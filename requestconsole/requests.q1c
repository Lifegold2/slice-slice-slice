<?xml version="1.0" encoding="UTF-8"?>
<querylist>
	<query name="вариант А">
		<text>ВЫБРАТЬ РАЗЛИЧНЫЕ
	НАЧАЛОПЕРИОДА(ПродажаТовара.Дата, ДЕНЬ) КАК Дата
ПОМЕСТИТЬ вт_ДатыПродаж
ИЗ
	Документ.ПродажаТовара КАК ПродажаТовара
ГДЕ
	ПродажаТовара.Дата МЕЖДУ &amp;НачалоПериода И &amp;КонецПериода
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	МАКСИМУМ(ЦеныНоменклатуры.Период) КАК Период,
	ЦеныНоменклатуры.Номенклатура КАК Номенклатура,
	вт_ДатыПродаж.Дата КАК Дата
ПОМЕСТИТЬ вт_ДатыАктуальныхЦен
ИЗ
	РегистрСведений.ЦеныНоменклатуры КАК ЦеныНоменклатуры
		ВНУТРЕННЕЕ СОЕДИНЕНИЕ вт_ДатыПродаж КАК вт_ДатыПродаж
		ПО ЦеныНоменклатуры.Период &lt;= вт_ДатыПродаж.Дата

СГРУППИРОВАТЬ ПО
	ЦеныНоменклатуры.Номенклатура,
	вт_ДатыПродаж.Дата
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	ПродажаТовара.Ссылка КАК Ссылка,
	ПродажаТовара.Контрагент КАК Контрагент,
	ПродажаТовараТовары.Номенклатура КАК Номенклатура,
	ПродажаТовараТовары.НомерСтроки КАК НомерСтроки,
	ПродажаТовараТовары.Количество КАК Количество,
	100 * (ЦеныНоменклатуры.Цена - ПродажаТовараТовары.Цена) / ЦеныНоменклатуры.Цена КАК Скидка
ПОМЕСТИТЬ ВременнаяТаблица
ИЗ
	Документ.ПродажаТовара КАК ПродажаТовара
		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПродажаТовара.Товары КАК ПродажаТовараТовары
		ПО (ПродажаТовара.Ссылка = ПродажаТовараТовары.Ссылка)
		ЛЕВОЕ СОЕДИНЕНИЕ вт_ДатыАктуальныхЦен КАК вт_ДатыАктуальныхЦен
		ПО (ПродажаТовараТовары.Номенклатура = вт_ДатыАктуальныхЦен.Номенклатура)
			И (НАЧАЛОПЕРИОДА(ПродажаТовара.Дата, ДЕНЬ) = вт_ДатыАктуальныхЦен.Дата)
		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры КАК ЦеныНоменклатуры
		ПО (ПродажаТовараТовары.Номенклатура = ЦеныНоменклатуры.Номенклатура)
			И (вт_ДатыАктуальныхЦен.Период = ЦеныНоменклатуры.Период)
ГДЕ
	ПродажаТовара.Дата МЕЖДУ &amp;НачалоПериода И &amp;КонецПериода
	И 100 * (ЦеныНоменклатуры.Цена - ПродажаТовараТовары.Цена) / ЦеныНоменклатуры.Цена &gt; 10
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	КОЛИЧЕСТВО(*) КАК Поле1
ИЗ
	ВременнаяТаблица КАК ВременнаяТаблица
</text>
		<parameters>
			<parameter name="НачалоПериода" type="Дата" value="2020-01-01T00:00:00"/>
			<parameter name="КонецПериода" type="Дата" value="2020-06-30T23:59:59"/>
		</parameters>
	</query>
	<query name="вариант Б">
		<text>ВЫБРАТЬ
	ЦеныНоменклатурыСрезПоследних.Период КАК Период,
	ЦеныНоменклатурыСрезПоследних.Номенклатура КАК Номенклатура,
	ЦеныНоменклатурыСрезПоследних.Цена КАК Цена
ПОМЕСТИТЬ вт_ЦеныВПериоде
ИЗ
	РегистрСведений.ЦеныНоменклатуры.СрезПоследних(&amp;НачалоПериода, ) КАК ЦеныНоменклатурыСрезПоследних

ОБЪЕДИНИТЬ ВСЕ

ВЫБРАТЬ
	ЦеныНоменклатуры.Период,
	ЦеныНоменклатуры.Номенклатура,
	ЦеныНоменклатуры.Цена
ИЗ
	РегистрСведений.ЦеныНоменклатуры КАК ЦеныНоменклатуры
ГДЕ
	ЦеныНоменклатуры.Период &gt; &amp;НачалоПериода
	И ЦеныНоменклатуры.Период &lt;= &amp;КонецПериода
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	ЦеныЗаПериод_Начало.Номенклатура КАК Номенклатура,
	ЦеныЗаПериод_Начало.Цена КАК Цена,
	ЦеныЗаПериод_Начало.Период КАК НачалоПериода,
	ЕСТЬNULL(МИНИМУМ(ЦеныЗаПериод_Конец.Период), &amp;КонецПериода) КАК КонецПериода
ПОМЕСТИТЬ вт_ПериодыЦен
ИЗ
	вт_ЦеныВПериоде КАК ЦеныЗаПериод_Начало
		ЛЕВОЕ СОЕДИНЕНИЕ вт_ЦеныВПериоде КАК ЦеныЗаПериод_Конец
		ПО ЦеныЗаПериод_Начало.Номенклатура = ЦеныЗаПериод_Конец.Номенклатура
			И ЦеныЗаПериод_Начало.Период &lt; ЦеныЗаПериод_Конец.Период

СГРУППИРОВАТЬ ПО
	ЦеныЗаПериод_Начало.Номенклатура,
	ЦеныЗаПериод_Начало.Цена,
	ЦеныЗаПериод_Начало.Период
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	ПродажаТовара.Ссылка КАК Ссылка,
	ПродажаТовара.Контрагент КАК Контрагент,
	ПродажаТовараТовары.Номенклатура КАК Номенклатура,
	ПродажаТовараТовары.Количество КАК Количество,
	100 * (вт_ПериодыЦен.Цена - ПродажаТовараТовары.Цена) / вт_ПериодыЦен.Цена КАК Скидка
ПОМЕСТИТЬ ВременнаяТаблица
ИЗ
	Документ.ПродажаТовара КАК ПродажаТовара
		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПродажаТовара.Товары КАК ПродажаТовараТовары
		ПО ПродажаТовара.Ссылка = ПродажаТовараТовары.Ссылка
		ЛЕВОЕ СОЕДИНЕНИЕ вт_ПериодыЦен КАК вт_ПериодыЦен
		ПО ПродажаТовара.Дата &gt;= вт_ПериодыЦен.НачалоПериода
			И ПродажаТовара.Дата &lt; вт_ПериодыЦен.КонецПериода
			И (ПродажаТовараТовары.Номенклатура = вт_ПериодыЦен.Номенклатура)
ГДЕ
	ПродажаТовара.Дата МЕЖДУ &amp;НачалоПериода И &amp;КонецПериода
//	И 100 * (вт_ПериодыЦен.Цена - ПродажаТовараТовары.Цена) / вт_ПериодыЦен.Цена &gt; 10
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	КОЛИЧЕСТВО(*) КАК Поле1
ИЗ
	ВременнаяТаблица КАК ВременнаяТаблица
	ГДЕ скидка &gt; 10
</text>
		<parameters>
			<parameter name="НачалоПериода" type="Дата" value="2020-01-01T00:00:00"/>
			<parameter name="КонецПериода" type="Дата" value="2020-06-30T23:59:59"/>
		</parameters>
	</query>
	<query name="вариант В">
		<text>ВЫБРАТЬ
	ПродажаТовара.Ссылка КАК Ссылка,
	ПродажаТовараТовары.Номенклатура КАК Номенклатура,
	ПродажаТовара.Контрагент КАК Контрагент,
	ПродажаТовараТовары.Количество КАК Количество,
	100 * (ЦеныНоменклатуры.Цена - ПродажаТовараТовары.Цена) / ЦеныНоменклатуры.Цена КАК Скидка
ПОМЕСТИТЬ ВременнаяТаблица
ИЗ
	Документ.ПродажаТовара.Товары КАК ПродажаТовараТовары
		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПродажаТовара КАК ПродажаТовара
		ПО (ПродажаТовараТовары.Ссылка = ПродажаТовара.Ссылка)
		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры КАК ЦеныНоменклатуры
		ПО (ПродажаТовараТовары.Номенклатура = ЦеныНоменклатуры.Номенклатура)
			И (ПродажаТовара.Дата &gt;= ЦеныНоменклатуры.Период)
		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры КАК ЦеныНоменклатуры2
		ПО (ПродажаТовараТовары.Номенклатура = ЦеныНоменклатуры2.Номенклатура)
			И (ПродажаТовара.Дата &gt;= ЦеныНоменклатуры2.Период)
			И (ЦеныНоменклатуры2.Период &gt; ЦеныНоменклатуры.Период)
ГДЕ
	ПродажаТовара.Дата МЕЖДУ &amp;НачалоПериода И &amp;КонецПериода
	И ЦеныНоменклатуры2.Период ЕСТЬ NULL  и 100 * (ЦеныНоменклатуры.Цена - ПродажаТовараТовары.Цена) / ЦеныНоменклатуры.Цена &gt; 10
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	КОЛИЧЕСТВО(*) КАК Поле1
ИЗ
	ВременнаяТаблица КАК ВременнаяТаблица
</text>
		<parameters>
			<parameter name="НачалоПериода" type="Дата" value="2020-01-01T00:00:00"/>
			<parameter name="КонецПериода" type="Дата" value="2020-06-30T23:59:59"/>
		</parameters>
	</query>
	<query name="вариант Г">
		<text>ВЫБРАТЬ
	ПродажаТовара.Ссылка КАК Ссылка,
	ПродажаТовара.Контрагент КАК Контрагент,
	ПродажаТовараТовары.Номенклатура КАК Номенклатура,
	ПродажаТовараТовары.Количество КАК Количество,
	100 * (ЦеныНоменклатуры.Цена - ПродажаТовараТовары.Цена) / ЦеныНоменклатуры.Цена КАК Скидка
ПОМЕСТИТЬ ВременнаяТаблица
ИЗ
	Документ.ПродажаТовара.Товары КАК ПродажаТовараТовары
		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПродажаТовара КАК ПродажаТовара
		ПО (ПродажаТовараТовары.Ссылка = ПродажаТовара.Ссылка)
		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры КАК ЦеныНоменклатуры
		ПО (ПродажаТовараТовары.Номенклатура = ЦеныНоменклатуры.Номенклатура)
			И (ПродажаТовара.Дата &gt;= ЦеныНоменклатуры.Период)
		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры КАК ЦеныНоменклатуры1
		ПО (ПродажаТовараТовары.Номенклатура = ЦеныНоменклатуры1.Номенклатура)
			И (ПродажаТовара.Дата &gt;= ЦеныНоменклатуры1.Период)
ГДЕ
	ПродажаТовара.Дата МЕЖДУ &amp;НачалоПериода И &amp;КонецПериода
	И 100 * (ЦеныНоменклатуры.Цена - ПродажаТовараТовары.Цена) / ЦеныНоменклатуры.Цена &gt; 10

СГРУППИРОВАТЬ ПО
	ПродажаТовараТовары.Номенклатура,
	ПродажаТовараТовары.Количество,
	ПродажаТовара.Контрагент,
	ЦеныНоменклатуры.Период,
	ПродажаТовара.Ссылка,
	100 * (ЦеныНоменклатуры.Цена - ПродажаТовараТовары.Цена) / ЦеныНоменклатуры.Цена,
	ПродажаТовараТовары.НомерСтроки

ИМЕЮЩИЕ
	МАКСИМУМ(ЦеныНоменклатуры1.Период) = ЦеныНоменклатуры.Период
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	КОЛИЧЕСТВО(*) КАК Поле1
ИЗ
	ВременнаяТаблица КАК ВременнаяТаблица
</text>
		<parameters>
			<parameter name="НачалоПериода" type="Дата" value="2020-01-01T00:00:00"/>
			<parameter name="КонецПериода" type="Дата" value="2020-06-30T23:59:59"/>
		</parameters>
	</query>
	<query name="вариант Д">
		<text>ВЫБРАТЬ
	ПродажаТовара.Ссылка КАК Ссылка,
	ПродажаТовараТовары.Цена КАК ЦенаДокумента,
	МИНИМУМ(РАЗНОСТЬДАТ(ЦеныНоменклатуры.Период, ПродажаТовара.Дата, ДЕНЬ) * &amp;Много + ЦеныНоменклатуры.Цена) - МИНИМУМ(РАЗНОСТЬДАТ(ЦеныНоменклатуры.Период, ПродажаТовара.Дата, ДЕНЬ)) * &amp;Много КАК Цена,
	ПродажаТовара.Контрагент КАК Контрагент,
	ПродажаТовараТовары.Номенклатура КАК Номенклатура,
	ПродажаТовараТовары.Количество КАК Количество,
	ПродажаТовараТовары.НомерСтроки КАК НомерСтроки
ПОМЕСТИТЬ ВременнаяТаблица
ИЗ
	Документ.ПродажаТовара.Товары КАК ПродажаТовараТовары
		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПродажаТовара КАК ПродажаТовара
		ПО (ПродажаТовараТовары.Ссылка = ПродажаТовара.Ссылка)
		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры КАК ЦеныНоменклатуры
		ПО (ПродажаТовараТовары.Номенклатура = ЦеныНоменклатуры.Номенклатура)
			И (ЦеныНоменклатуры.Период &lt;= ПродажаТовара.Дата)
ГДЕ
	ПродажаТовара.Дата МЕЖДУ &amp;НачалоПериода И &amp;КонецПериода

СГРУППИРОВАТЬ ПО
	ПродажаТовара.Ссылка,
	ПродажаТовараТовары.Номенклатура,
	ПродажаТовараТовары.Цена,
	ПродажаТовара.Контрагент,
	ПродажаТовараТовары.Количество,
	ПродажаТовараТовары.НомерСтроки

ИМЕЮЩИЕ
	100 * (МИНИМУМ(РАЗНОСТЬДАТ(ЦеныНоменклатуры.Период, ПродажаТовара.Дата, ДЕНЬ) * &amp;Много + ЦеныНоменклатуры.Цена) - МИНИМУМ(РАЗНОСТЬДАТ(ЦеныНоменклатуры.Период, ПродажаТовара.Дата, ДЕНЬ)) * &amp;Много - ПродажаТовараТовары.Цена) / (МИНИМУМ(РАЗНОСТЬДАТ(ЦеныНоменклатуры.Период, ПродажаТовара.Дата, ДЕНЬ) * &amp;Много + ЦеныНоменклатуры.Цена) - МИНИМУМ(РАЗНОСТЬДАТ(ЦеныНоменклатуры.Период, ПродажаТовара.Дата, ДЕНЬ)) * &amp;Много) &gt; 10
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	КОЛИЧЕСТВО(*) КАК Поле1
ИЗ
	ВременнаяТаблица КАК ВременнаяТаблица
</text>
		<parameters>
			<parameter name="НачалоПериода" type="Дата" value="2020-01-01T00:00:00"/>
			<parameter name="КонецПериода" type="Дата" value="2020-03-31T23:59:59"/>
			<parameter name="Много" type="Число" value="100000"/>
		</parameters>
	</query>
</querylist>